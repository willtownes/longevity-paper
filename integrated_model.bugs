#Full model for integrating gene expression with survival outcome
### input data ###
#xx an (integer) indicator vector of perturbations for gene expression
#xs an indicator vector of perturbations for survival
#Nx length of xx (number of gene expression perturbations)
#Ns length of xs (number of survival perturbations
#ncell_s integer vector of length Ns, number of cells for each survival pert
#ncell_x integer, number of cells for gene expression (assumed const acros perts)
#L number of latent dimensions
#K number of gene clusters
#G number of total genes
#W a matrix with G rows, Nx cols of gene expression measurements
#columns of W= perturbations, rows of W= expression of genes
#y a vector of length Ns of average survival outcomes for each perturbation
#dir_wts a vector of length K of dirichlet prior weights for gene clusters

### primary parameters ###
#Us a matrix of Ns rows and L columns of latent factors for survival perts
#Ux a matrix of Nx rows and L columns of latent factors for gene expression perts
#V a matrix of G rows and L columns of gene expression loadings
#b a vector of length L of sparse regression coefficients for survival 
#A a matrix of G rows and L columns of latent factors per gene
#z an (integer) indicator vector of gene cluster memberships (length=G)
#Phi a matrix of K rows and L columns of prototypes for A
#Psi a matrix of K rows and L columns of prototypes for V
#theta a vector of cluster membership probabilities

### secondary parameters ###
#mu_y,tau_y intercept and precision params for survival
#mu_w,tau_w intercept and precision params for gene expression

model{
  # Likelihoods
  for(n in 1:Ns){
    y[n] ~ dnorm(mu_y+inprod(b,Us[n,]), tau_y*ncell_s[n])
    Us[n,]<- A[xs[n],]
  }
  for(g in 1:G){
    for(n in 1:Nx) {
      W[g,n] ~ dnorm(mu_w[g]+inprod(Ux[n,], V[g,]), tau_w*ncell_x)
    }
    for(l in 1:L){
      V[g,l]~dnorm(Psi[z[g],l], tau_v)
      A[g,l]~dnorm(Phi[z[g],l], tau_a)
    }
    z[g]~dcat(theta)
  }
  #Priors for survival model
  tau_y ~ dgamma(.001,.001)
  mu_y ~ dt(0,0.001,4)
  
  #Priors for factor model
  tau_v~dgamma(.001,.001)
  tau_a~dgamma(.001,.001)
  tau_w~dgamma(.001,.001)
  for(g in 1:G){
    mu_w[g]~dt(0,.001,4)
  }
  
  #Priors for finite mixture part
  for(k in 1:K){
    for(l in 1:L){
      Phi[k,l]~dnorm(0,tau_phi)
      Psi[k,l]~dnorm(0,tau_psi)
    }
  }
  theta ~ ddirch(dir_wts)
  tau_phi ~ dgamma(.001,.001)
  tau_psi ~ dgamma(.001,.001)
  
  #Priors for spike and slab regression part
  for(l in 1:L){
    gamma[l] ~ dnorm(0,tau_gamma) #slab
    delta[l] ~ dbern(omega)
    b[l]  <- gamma[l]*delta[l]
  }

  omega ~ dbeta(1,9) #about 10% of coefficients expected to be nonzero
  tau_gamma ~ dgamma(.1,.1)
}